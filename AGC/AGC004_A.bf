### AGC004_C: Divide a Cuboid
memory layout: ___A___B___C_____P___

read num: A
>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
read num: B
>>[>]>>
>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
read num: C
>>[>]>>
>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
compare: B C
<<<<[<]
>[[<<+>>[>]>+<<[<]>-]>[>]>>>[>]>>[<<+[<]<->>[>]>-]
<<[<]<[<->>+<-[-[-[-[-[-[-[-[-[[+]>+<]]]]]]]]]]
<+[-<[<]]>]>[-<]>[-<+>]
<<<[<]<<[[->>+<<]<]>>>[>]>>>[>]>>[[-<<+>>]>]<<<[<]
swap: B C
<[->>[[-<<+>>]<<[<]<[<]>[[>]>[>]>+<<[<]<[<]>-]
>[>]>[>]<[[<]<[<]<+>>[>]>[>]<-]>>[-<<+>>]>]
<<<[[->>+<<]<]<<[[->+<]<]
>>[>]>]
compare: A B
<<[<]<<<<<[<]
>[[<<+>>[>]>+<<[<]>-]>[>]>>>[>]>>[<<+[<]<->>[>]>-]
<<[<]<[<->>+<-[-[-[-[-[-[-[-[-[[+]>+<]]]]]]]]]]
<+[-<[<]]>]>[-<]>[-<+>]
<<<[<]<<[[->>+<<]<]>>>[>]>>>[>]>>[[-<<+>>]>]<<<[<]
swap: A B
<[->>[[-<<+>>]<<[<]<[<]>[[>]>[>]>+<<[<]<[<]>-]
>[>]>[>]<[[<]<[<]<+>>[>]>[>]<-]>>[-<<+>>]>]
<<<[[->>+<<]<]<<[[->+<]<]
>>[>]>]
calc %2: A B C
<<[->+>>+<<<]>[-<+>]->>[<+>-[-<-]+[-<+]->>]
>[>]<[->+>>+<<<]>[-<+>]->>[<+>-[-<-]+[-<+]->>]
>>>[>]<[->+>>+<<<]>[-<+>]->>[<+>-[-<-]+[-<+]->>]
<<+>[[-<+]->[-]+>>>>[>]>]<<[<]<<<<+>[[-<+]->[-]+>>[>]>]
if(product % 2 == 0) print(0)
+[-<+]->[[+++++<+>]<--.[-]>]
else
<[+
multiply B by C
set 0: P
>>>[>]<[[->>+<<]<]>>>[>]>>>>>[>]>>>>
>+>+>+>+>+ >+>+>+>+>+ >+>+>+>+>+ >+>+>+>+>+[<]
add B many times: P
<<<<<[->>+<<[[<]<<<[
->>+<<[>>+[>]>[>]>[>]>>>[>]<+[<]<<<[<]<[<]<[<]<-]
>>[>]>[>]>[>]>>>[>]<[->+<]<[<]<<<[<]<[<]<[<]<<]
>>>[[-<<+>>]>]>[>]>+[>]>>>[>]>[[-<+>]>]<<[<]<<<[<]<-]
carry up: P
>>[>]>>>[[-<+>]>]<<
[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
[->+<]<[+>]>[<]>----- -----[-<+>]]]]]]]]]]]<]
next digit: C
>>[>]<[->>>>+<<<<]<[<]<<<[<]<<]
adjust: C P
>>>[[-<<+>>]>]>>>[>]>>>>[[-<<<<+>>>>]>]
write num: P
<<<<<[<]>[-[+<<]>]>[<+[+++++>+<]>----.[-]>]]
