### ChokudaiSpeedRun002_D: 和の最大値β
memory layout: ___S___A___B___

set
,[,----- -----]
>>>+>+>+ >+>+>+>+>+ >+>+>+>+>+ >+>+
read num
>>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
non zero judge
>>[-[+[<]<]+>]>[[>]>]<<[<]
while
<[-
read num
>>>[>]>>
>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
compare
<<<<[<]>[[<<+>>[>]>+<<[<]>-]>[>]>>>[>]>>[<<+[<]<->>[>]>-]
<<[<]<[<->>+<-[-[-[-[-[-[-[-[-[[+]>+<]]]]]]]]]]
<+[-<[<]]>]>[-<]>[-<+>]
<<<[<]<<[[->>+<<]<]>>>[>]>>>[>]>>[[-<<+>>]>]<<<[<]
if
<[-
A = B
<<[[-]+<]>[>]>>>[-[-<<<<< <<<<< <<<+>>>>> >>>>> >>>]+>]
end if
<[<]<]
add max(A B) to S
<<[-[-<<<<< <<<<< <<<+>>>>> >>>>> >>>]+<]
carry up
<<<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
[->+<]<[+>]>[<]>----- -----[-<+>]]]]]]]]]]]<]
clear
>>[[-<+>]>]>->[->]>>>[[-]>]+[-<+]
read num
->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+>+ >+>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
non zero judge
>>[-[+[<]<]+>]>[[>]>]<<[<]
end while
<]
output
<<[<]>[-[+<<]>]>[<+[+++++>+<]>----.[-]>]
