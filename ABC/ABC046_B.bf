### ABC046_B: Painting Balls with AtCoDeer
memory layout: ___N___P___K___Q___

read num: N
>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
decrement: N
>>[>]+[<[->->+<<]+>[[+]<->]<]>>[-[-<<+>>]]>[[[-]<<+++++ +++++>>]>]
set: P
>+>+>+>+>+ >+>+>+>+>+
read num: K
>>>->>,+[----- ----- -[-->+++++[-<---->]<[
>+++++[-<--->],+<[-<+>]]]>]
+[-<+]>+>+>+>+
[-[<[-<]<]<[[->+<]<]>>[<++[>]]<]
add K: P
>>[>]<[->>+<<[[<]<<[<]<+>>[>]>>[>]>+<<-]<[<]<<[<]<[->+<]>[>]>>[>]<]
<<[<]>[[-<+>]>]>>>>[[-<<+>>]>]
decrement: K
<<+[<[->->+<<]+>[[+]<->]<]>>[-[-<<+>>]]>[[[-]<<+++++ +++++>>]>]
set: Q
>+>+>+>+>+ >+>+>+>+>+ >+>+>+>+>+[<]
while: N
<<<[<]<<<[<]<<<[-[+[>]>]+<]<[[<]<]>>[>]>[-
decrement: N
<+[<[->->+<<]+>[[+]<->]<]>>[-[-<<+>>]]>[[[-]<<+++++ +++++>>]>]
multiply K by P: Q
>[>]<[-[
add K: Q
>>[>]>>[>]<[->>+<<[>>+[>]>[>]<+[<]<[<]<-]>>[>]>[>]<[->+<]<[<]<[<]<<]
>>>[[-<<+>>]>]>[[-<+>]>]>[[-<<+>>]>]
<<<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
[->+<]<[+>]>[<]>----- -----[-<+>]]]]]]]]]]]<]
<<[<]<<[<]<-]
end multiply
>>[>]>>[>]>>>[>]<[->>+<<]<[<]<<<[<]<<[<]+<<]
add Q: P
+>+[>]<-<-<[
->+[>]>>>[>]>>>[>]>>[>]<
[-[[<]<<[<]<<<[<]<<<[<]>+[>]>>>[>]>>>[>]>>[>]<-]]
<[<]<<[<]<<<[<]<<<[<]<]
>>[>]>>>[>]>>>
[>]+>+>+>+>+ >+>+>+>+>+[<]
end while: N
<<<[<]<<<[<]<<<[-[+[>]>]+<]<[[<]<]>>[>]>]
write num: P
>>[-[+<<]>]>[<+[+++++>+<]>----.[-]>]
