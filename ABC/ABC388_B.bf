### ABC388_A: Heavy Snake
memory layout: __d___0___o___O___{T___A___P___}

skip until space
+[,>+++++ +++[-<---->]<]
read single num: d
,----- -----[
[->+<]+++++[->----<]>--[
>[-<+++++ +++++>]
++++[-<---->]<[->+<]
<,----- ----->]<]
set 0: 0 o O
->>>>>>+>+>+
>>>>+>+>+>+>+
>>>>+>+>+>+>+
read num: T
>>>>+>+>+
>>,+[----- ----- -[<+++++[->----<]>--[
[-<+>]+++++[-<--->]<[<]>->[[-<+>]>],+<]]>]<<[>]
while: T
<[-[+[>]>]+<]<[[<]<]>>[>]>[-
read num: A
>>>>+>+>+
>>,+[----- ----- -[<+++++[->----<]>--[
[-<+>]+++++[-<--->]<[<]>->[[-<+>]>],+<]]>]<<[>]
multiply T by A: P
>>>+>+>+>+>+[<]<<<[<]<<<<<[-[->>+[>]
>>>>>[-[->>+[>]>[>]<+[<]<[<]<]>>+[>]>[>]<[->+<]<[<]<[<]<<]
>>>[[-<<+>>]>]>[>]>[[-<+>]>]<<[<]<[<]<<<[<]<<<[<]<]
>>+[>]>>>[>]>>>[[-<+>]>]
<<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<[->+<]<[+>]>[<]>[-<+>]]]]]]]]]]]<]
>>[>]<[->>+<<]<[<]<<<[<]<<<[<]<<]
>>>[[-<<+>>]>]>+>+[>]>>>[>]>>[[-<<+>>]>]
read num: T
>+>+>+
>>,+[----- ----- -[<+++++[->----<]>--[
[-<+>]+++++[-<--->]<[<]>->[[-<+>]>],+<]]>]<<[>]
end while: T
<[-[+[>]>]+<]<[[<]<]>>[>]>]
clear: T_last
<<[-<]
while: d
+[-<+]->>[-
for each: T
>>>>> >>>>> >>>>> >>>>> >>>>> >[
add T: P
[-<+>[-<+>>>>> >>>>> >>>>> >>+<<<<< <<<<< <<<<< <]>]
<<[[->+<]<]>>[>]>>>[[-]>]>>>[>]
<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<[->+<]<[+>]>[<]>[-<+>]]]]]]]]]]]<]
copy P: A
>>[[-<+<<<<< <<<+>>>>> >>>>]>]
subtract A_prev: A_curr
<<<<< <<<<<[+++++ +++++<]
<<<<< <<<<< <<<<< <<[[->+>>>>> >>>>> >>>>> >>>>> >-<<<<< <<<<< <<<<< <<<<< <<]<]
>>[[-<+>]>]>>>>> >>>>> >>>>> >[[-<+>]>]
<<+[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<[->+<]<[+>]>[<]>[-<+>]]]]]]]]]]]<]
if(A_curr lt 0) set A_prev: A_curr
+>>[-<+>[-<+>[-<+>[-<+>[-<+>[
<[[-]>]<<<<< <<<<< <<<<< <<<<< <<<[[
->+>>>>> >>>>> >>>>> >>>>> >+<<<<< <<<<< <<<<< <<<<< <<]<]
>>[[-<+>]>]>>>>> >>>>> >>>>->>[-<+>]]]]]]]
if(A_curr ge 0) set P_curr: A_curr
<[->+<]<[->>[[-]>]>>>[[-<+<<<<< <<+>>>>> >>>]>]<<[[->+<]<]<<<<< <<<]
end for each: T
>>>>> >>>>> >>>>> >>>]
write num: A_last
<<<<< <<<<< <<<<< <[-[+<<]>]>[<+[+++++>+<]>----.[-]>]
+++++ +++++.[-]<+<+<+<+<+
end while: d
+[-<+]->>]
