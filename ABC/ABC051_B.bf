### ABC051_B: Sum of Three Integers
memory layout: ___X___K___S_f_A___

read num: X
>>+>+>+>+>+
>>,+[----- ----- -[<+++++[->----<]>--[[-<+>]+++++[-<--->]<[<]>->[[-<+>]>],+<]]>]<<[>]
copy X: K
<[[->+>>>>> >>>+<<<<< <<<<]<]
increment: X
>>[>]+[+++++ ++++<[->->+<<]+>[[-]<->]<]>>[+[-<<+>>]]>[[[-]<<+>>]>]
read num: S
>[>]>>>+>+>+>+>+
>>,+[----- ----- -[<+++++[->----<]>--[[-<+>]+++++[-<--->]<[<]>->[[-<+>]>],+<]]>]<<[>]
set 0: A
>>>+>+>+>+>+ >+>+>+>+>+[<]
while: X
<<<[<]<<<[<]<+[-
decrement: X
<+[<[->->+<<]+>[[+]<->]<]>>[-[-<<+>>]]>[[[-]<<+++++ +++++>>]>]
if(S lt 0): *__S
>[>]>->>-[+<]+<[+>-]+<+[->-
set 0: X & end if: *__S
<<<[<]<<<[[-]+<]>[>]>>>[>]>]
else: *_S
>[-
subtract K: S
>[+++++ +++++>]<[<]<<<[[->+>>>>> >>-<<<<< <<<]<]>>[>]>>[[-<+>]>]
<<+[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
[->+<]<[+>]>[<]>----- -----[-<+>]]]]]]]]]]]<]
set (S lt 0): f
+>>-[+<]+<[>->[>]>+<<[<]]<-
add K: S
<<<<<[-<+>[-<+>>>>> >>>+<<<<< <<]>]>>[[-<+>]>]
<<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<<[+>>]<[<]>>[>]]]]]]]]]]]<]
if(S lt K): f
>>[>]+>[-<-
add S & increment: A
<[->+<[->+>>>>> >>>>> >>+<<<<< <<<<< <<<]<]>>[[-<+>]>]>>[[-<+>]>]
<<+[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<<[+>>]<[<]>>[>]]]]]]]]]]]<]
end if: f
]
else: S*_f
<[-
subtract 2K & decrement: S
<[+++++ +++++ +++++ +++++ +++++ ++++<]
<<<[[->+>>>>> >>--<<<<< <<<]<]>>[[-<+>]>]>>[[-<+>]>]
<<++[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<<[+>>]<[<]>>[>]]]]]]]]]]]<]
if(S lt 0): *__S
->>-[+<]+<[+>-]<+[-
subtract S: A
>>[>]>>>[+++++ +++++>]<[<]<<<[[->+>>>>> >>>>> >>-<<<<< <<<<< <<<]<]
>>[[-<+>]>]+++++[[>]>[-<+>]<[<]>-]>[----- ----->]>[[-<+>]>]
<<+[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<<[+>>]<[<]>>[>]]]]]]]]]]]<]
end if: *__S
<<[<]<]
add 2K & increment: S
<<[->+<[->+>>>>> >>++<<<<< <<<]<]>>[[-<+>]>]>>[[-<+>]>]
<<+[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[->+<[
>[-]<<[+>>]<[<]>>[>]]]]]]]]]]]<]
end else: S*_f
>>[>]]
end else: *_S
<[<]<]
decrement: S
>>[>]+[<[->->+<<]+>[[+]<->]<]>>[-[-<<+>>]]>[[[-]<<+++++ +++++>>]>]
end while: X
<<<[<]<<<[<]<<<[-[+[>]>]+<]<[[<]<]>>[>]>]
write num: A
>>[>]>>>[>]>>>[-[+<<]>]>[<+[+++++>+<]>----.[-]>]+++++ +++++.
